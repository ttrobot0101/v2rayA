; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "v2rayA"
#define MyAppVersion "TheRealVersion"
#define MyAppPublisher "v2rayA Organization"
#define MyAppURL "https://v2raya.org/"
#define MyAppExeName "v2raya.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{B991C73A-599E-460D-A35A-D1ED414DADBF}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
ArchitecturesAllowed=arm64
ArchitecturesInstallIn64BitMode=arm64
DefaultDirName={autopf}\{#MyAppName}
; DisableDirPage=yes
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=D:\LICENSE.txt
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir=D:\
OutputBaseFilename=installer_windows_inno_arm64
Compression=lzma
SolidCompression=yes
UninstallDisplayName={#MyAppName}-{#MyAppVersion}
WizardStyle=modern
SetupIconFile=D:\v2raya.ico
MinVersion=10.0.14393

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "chinesesimplified"; MessagesFile: "ChineseSimplified.isl"

[Files]
Source: "D:\v2raya-arm64-windows\bin\{#MyAppExeName}"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "D:\v2raya-arm64-windows\bin\*"; DestDir: "{app}\bin"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "D:\v2raya-arm64-windows\data\*"; DestDir: "{app}\data"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "D:\v2raya.ico"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{commondesktop}\v2rayA Web Panel"; Filename: "http://localhost:2017"; IconFilename: "{app}\v2raya.ico";
Name: "{group}\v2rayA Web Panel"; Filename: "http://localhost:2017"; IconFilename: "{app}\v2raya.ico";
Name: "{group}\v2rayA Wiki"; Filename: "{#MyAppURL}";
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}";

[Run]
Filename: "sc.exe"; Parameters: "create v2rayA binPath= """{app}\bin\v2raya.exe""" DisplayName= ""v2rayA Service"" start= auto"; Flags: runhidden waituntilterminated
Filename: "sc.exe"; Parameters: "description v2rayA ""v2rayA - A web GUI client of Project V"""; Flags: runhidden waituntilterminated
Filename: "cmd.exe"; Parameters: "/C setx V2RAYA_CONFIG ""{app}"" /M"; Flags: runhidden waituntilterminated
Filename: "cmd.exe"; Parameters: "/C setx V2RAYA_V2RAY_ASSETSDIR ""{app}\data"" /M"; Flags: runhidden waituntilterminated
Filename: "cmd.exe"; Parameters: "/C setx V2RAYA_PASSCHECKROOT ""true"" /M"; Flags: runhidden waituntilterminated
Filename: "sc.exe"; Parameters: "start v2rayA"; Flags: runhidden waituntilterminated

[UninstallRun]
Filename: "sc.exe"; Parameters: "stop v2rayA"; Flags: runhidden
Filename: "sc.exe"; Parameters: "delete v2rayA"; Flags: runhidden
Filename: "cmd.exe"; Parameters: "/C reg delete HKLM\SYSTEM\CurrentControlSet\Control\Session Manager\Environment /v V2RAYA_CONFIG /f"; Flags: runhidden
Filename: "cmd.exe"; Parameters: "/C reg delete HKLM\SYSTEM\CurrentControlSet\Control\Session Manager\Environment /v V2RAYA_V2RAY_ASSETSDIR /f"; Flags: runhidden
Filename: "cmd.exe"; Parameters: "/C reg delete HKLM\SYSTEM\CurrentControlSet\Control\Session Manager\Environment /v V2RAYA_PASSCHECKROOT /f"; Flags: runhidden

[UninstallDelete]
Type: filesandordirs; Name: "{app}\bolt.db"
Type: filesandordirs; Name: "{app}\bin\v2raya.log"

[Code]
function InitializeSetup() : Boolean;
var 
  ResultCode: Integer;
begin
  Result := True;
  Exec('cmd.exe', '/C sc.exe stop v2rayA', '', SW_HIDE, ewWaitUntilTerminated, ResultCode); 
end;